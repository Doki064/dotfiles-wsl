# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block; everything else may go below.
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
  source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

# Load options
source $ZDOTDIR/.options

# Load functions
source $ZDOTDIR/.functions

# Load aliases
source $ZDOTDIR/.aliases

### Added by Zinit's installer
if [[ ! -f $HOME/.zinit/bin/zinit.zsh ]]; then
    print -P "%F{33}▓▒░ %F{220}Installing %F{33}DHARMA%F{220} Initiative Plugin Manager (%F{33}zdharma/zinit%F{220})…%f"
    command mkdir -p "$HOME/.zinit" && command chmod g-rwX "$HOME/.zinit"
    command git clone https://github.com/zdharma/zinit "$HOME/.zinit/bin" && \
        print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
        print -P "%F{160}▓▒░ The clone has failed.%f%b"
fi

source "$HOME/.zinit/bin/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load colors
autoload -U colors
colors

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
      zinit-zsh/z-a-rust \
      zinit-zsh/z-a-as-monitor \
      zinit-zsh/z-a-patch-dl \
      zinit-zsh/z-a-bin-gem-node

# Load theme and plugins without Turbo
zinit for \
      agkozak/zsh-z \
  light-mode depth"1" \
      romkatv/powerlevel10k

# Load a few useful plugins for zsh with Turbo
zinit wait lucid for \
  blockf atpull"zinit creinstall -q ." \
      zsh-users/zsh-completions

zinit wait lucid for \
      OMZP::colored-man-pages \
      OMZP::command-not-found

zinit wait lucid for \
      OMZP::sudo \
      OMZP::pipenv \
      OMZP::git \
      OMZL::git.zsh

zinit wait lucid as"null" for \
  atclone"./libexec/pyenv init --path > zpyenv.path.zsh && ./libexec/pyenv init - > zpyenv.zsh" \
  atinit'export PYENV_ROOT="$PWD"' atpull"%atclone" \
  multisrc"zpyenv.path.zsh zpyenv.zsh" nocompile"!" sbin"bin/pyenv" \
      pyenv/pyenv \
  atclone'ln -sfn "$PWD" "$PYENV_ROOT/plugins/pyenv-virtualenv" && ./bin/pyenv-virtualenv-init - > zpyenv.venv.zsh' \
  atpull"%atclone" src"zpyenv.venv.zsh" \
      pyenv/pyenv-virtualenv

zinit wait lucid as"null" from"gh-r" for \
  sbin"bin/exa" \
      ogham/exa

zinit wait lucid reset \
  atclone"[[ -z ${commands[dircolors]} ]] && local P=g
    \${P}sed -i '/DIR/c\DIR 38;5;63;1' LS_COLORS; \
    \${P}dircolors -b LS_COLORS > clrs.zsh" \
  atpull'%atclone' pick"clrs.zsh" nocompile'!' \
  atload'zstyle ":completion:*" list-colors "${(s.:.)LS_COLORS}"' for \
      trapd00r/LS_COLORS

zinit wait"1a" lucid light-mode for \
  atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay" \
      Aloxaf/fzf-tab

zinit wait"1" lucid for \
  atload"!_zsh_autosuggest_start" \
      zsh-users/zsh-autosuggestions \
  light-mode \
      zdharma/fast-syntax-highlighting

### End of Zinit's installer chunk


# To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
[[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
